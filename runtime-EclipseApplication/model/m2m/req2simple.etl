
pre {
	var m = new Out!Model;
}

@abstract
rule Identifiable2Identifiable
	transform i1 : In!Identifiable
	to i2 : Out!Identifiable{

	i2.id = i1.id;	
}

@abstract
rule Describable2Describable
	transform d1 : In!Describable
	to d2 : Out!Describable{

	d2.description = d1.description;	
}

@abstract
rule Requirements2CompletedRequirements
	transform r : In!Requirement
	to s : Out!Requirement
	extends Identifiable2Identifiable, Describable2Describable{
	
	guard : r.progress < 100
	
	s.progress = r.progress;
	s.conflictsOutgoing = r.conflictsOutgoing.select(d|d.progress < 100).equivalent();
	s.conflictsIncoming = r.conflictsIncoming.select(d|d.progress < 100).equivalent();
	s.parents = r.parents.select(d|d.progress < 100).equivalent();
	s.children = r.children.select(d|d.progress < 100).equivalent();
}

rule CustomerRequirements2CompletedRequirements
	transform r : In!CustomerRequirement
	to s : Out!CustomerRequirement
	extends Requirements2CompletedRequirements {
	
	m.requirements.add(s);
}

rule SystemRequirements2CompletedRequirements
	transform r : In!SystemRequirement
	to s : Out!SystemRequirement
	extends Requirements2CompletedRequirements{
	
	s.testCases = r.testCases.equivalent();
	m.requirements.add(s);
}

rule TestCase2TestCase
	transform tc1 : In!TestCase
	to tc2 : Out!TestCase
	extends Identifiable2Identifiable, Describable2Describable{
	
	guard : tc1.requirements.exists(r|r.progress < 100)
	
	tc2.requirements = tc2.requirements.equivalent();
	
	m.testCases.add(tc2);
}
	
rule TeamMember2TeamMember
	transform tm1 : In!TeamMember
	to tm2 : Out!TeamMember 
	extends Identifiable2Identifiable{
	
	guard : tm1.requirements.exists(r|r.progress < 100)
	
	tm2.requirements = tm1.requirements.equivalent();
	
	m.teamMembers.add(tm2);	
}	
	